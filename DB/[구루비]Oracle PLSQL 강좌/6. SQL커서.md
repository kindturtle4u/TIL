> 출처 : [gurubee.net](http://gurubee.net/oracle/plsql) Oracle PL/SQL 강좌 읽으면서 정리

# 6. SQL 커서
## 6.1 암시적 커서(Implicit Cursor)
#### 암시적 커서(Implicit Cursor)란?
- 암시적인 커서는 오라클이나 PL/SQL 실행 메커니즘에 의해 처리되는 SQL문장이 처리되는 곳에 대한 익명의 주소이다.
- 오라클 데이터베이스에서 실행되는 **모든 SQL문장은 암시적인 커서가 생성**되며, 커서 속성을 사용할 수 있다.
- 암시적 커서는 SQL 문이 실행되는 순간 **자동으로 OPEN과 CLOSE를 실행**한다.

#### 암시적 커서의 속성
- SQL%ROWCOUNT : 해당 SQL 문에 영향을 받는 행의 수
- SQL%FOUND : 해당 SQL 영향을 받는 행의 수가 한 개 이상일 경우 TRUE
- SQL%NOTFOUND : 해당 SQL 문에 영향을 받는 행의 수가 없을 경우 TRUE
- SQL%ISOPEN : 항상 FALSE, 암시적 커서가 열려 있는지의 여부 검색

#### 암시적 커서(Implicit Cursor) 예제
```sql
SQL> CREATE OR REPLACE PROCEDURE Implicit_Cursor
        (p_empno IN emp.empno%TYPE)

    IS

        v_sal  emp.sal%TYPE;
        v_update_row NUMBER;

    BEGIN

        SELECT sal
        INTO v_sal
        FROM emp
        WHERE empno = p_empno;

        -- 검색된 데이터가 있을경우
        IF  SQL%FOUND THEN     
            DBMS_OUTPUT.PUT_LINE('검색한 데이터가 존재합니다 : '||v_sal);
        END IF;

        UPDATE emp
        SET sal = sal*1.1
        WHERE empno = p_empno;

        -- 수정한 데이터의 카운트를 변수에 저장
        v_update_row := SQL%ROWCOUNT;
        DBMS_OUTPUT.PUT_LINE('급여가 인상된 사원 수 : '|| v_update_row);
        
        EXCEPTION    
           WHEN   NO_DATA_FOUND  THEN  
           DBMS_OUTPUT.PUT_LINE(' 검색한 데이터가 없네요... ');
        
    END;
    /
 
-- DBMS_OUTPUT.PUT_LINE을 출력하기 위해 사용
SQL> SET SERVEROUTPUT ON ;  

-- 프로시저 실행
SQL> EXECUTE Implicit_Cursor(7369);

검색한 데이터가 존재합니다 : 880
급여가 인상된 사원 수 : 1
```
## 6.2 명시적 커서(Explicit Cursor)
### 6.2.1 명시적 커서(EXPLICIT CURSOR)
#### 커서란 무엇인가?
- 커서는 Private SQL의 작업 영억이다.
- 오라클 서버에 의해 실행되는 모든 SQL문은 연관된 각각의 커서를 소유하고 있다.
- 커서의 종류
    * 암시적 커서 : 모든 DML과 PL/SQL SELECT문에 대해 선언된다.
    * 명시적 커서 : 프로그래머에 의해 선언되며 이름이 있는 커서이다.
    
#### 문법
```sql
CURSOR cursor_name IS
    SELECT statement
```

#### 커서열기(OPEN)
- 커서 열기는 OPEN문을 사용한다.
- 커서안의 검색이 실행되며 아무런 데이터행을 추출하지 못해도 에러가 발생하지 않는다.
- 문법 : OPEN cursor_name

#### 커서 패치(FETCH)
- 커서의 FETCH는 현재 데이터 행을 OUTPUT 변수에 반환 한다.
- 커서의 SELECT문의 컬럼의 수와 OUTPUT변수의 수가 동일해야 한다.
- 커서 컬럼의 변수의 타입과 OUTPUT변수의 데이터 타입도 동일해야 한다.
- 커서는 한 라인씩 데이터를 패치한다.
- 문법 FETCH cursor_name INTO variable1, variable2;

#### 커서 닫기(CLOSE)
- 사용을 마친 커서는 반드시 닫아 주어야 한다.
- 필요하다면 커서를 다시 열 수 있다.
- 커서를 닫은 상태에서 FETCH를 할 수 없다.
- 문법 : CLOSE cursor_name

#### Explicit Cursor 예제
```sql
-- 특정 부서의 평균급여와 사원수를 출력..
SQL> CREATE OR REPLACE PROCEDURE ExpCursor_Test 
       (v_deptno IN  dept.deptno%TYPE)
       
     IS

       CURSOR dept_avg IS
       SELECT b.dname, COUNT(a.empno) cnt, 
               ROUND(AVG(a.sal),3) salary
       FROM emp a, dept b
       WHERE a.deptno = b.deptno
         AND b.deptno = v_deptno
       GROUP BY b.dname ;

       -- 커서를 패치하기 위한 편수 선언
       v_dname   dept.dname%TYPE;
       emp_cnt   NUMBER;
       sal_avg   NUMBER;

     BEGIN

       -- 커서의 오픈
       OPEN dept_avg;

       -- 커서의 패치 
       FETCH dept_avg INTO v_dname, emp_cnt, sal_avg;
  
       DBMS_OUTPUT.PUT_LINE('부서명 : ' || v_dname);
       DBMS_OUTPUT.PUT_LINE('사원수 : ' || emp_cnt);
       DBMS_OUTPUT.PUT_LINE('평균급여 : ' || sal_avg);

       -- 커서의 CLOSE
       CLOSE dept_avg;      

    EXCEPTION
      WHEN OTHERS THEN
        DBMS_OUTPUT.PUT_LINE(SQLERRM||'에러 발생 ');

   END;
   /

-- DBMS_OUTPUT.PUT_LINE을 출력하기 위해 사용
SQL> SET SERVEROUTPUT ON ;

SQL> EXECUTE ExpCursor_Test(30);
부서명 : SALES
사원수 : 6
평균급여 : 1550.833
```

### 6.2.2 FOR문에서 커서 사용(Cursor FOR Loops)
FOR문을 사용하면 커서의 ** OPEN, FETCH, CLOSE가 자동 발생**하므로 따로 기술할 필요가 없고, 
레코드 이름도 자동 선언되므로 따로 선언할 필요가 없다.

#### FOR문에서 커서 사용 문법
FOR record_name IN cursor_name LOOP
    statement1
    statement2
    ...
END LOOP;

#### FOR문에서 커서 사용 예제
```sql
SQL> CREATE OR REPLACE PROCEDURE ForCursor_Test
     IS

        -- Cursor 선언
        CURSOR dept_sum IS
        SELECT b.dname, COUNT(a.empno) cnt, SUM(a.sal) salary
        FROM emp a, dept b
        WHERE a.deptno = b.deptno
        GROUP BY b.dname;

     BEGIN

       -- Cursor를 FOR문에서 실행시킨다
       FOR emp_list IN dept_sum LOOP
          DBMS_OUTPUT.PUT_LINE('부서명 : ' || emp_list.dname);
          DBMS_OUTPUT.PUT_LINE('사원수 : ' || emp_list.cnt);
          DBMS_OUTPUT.PUT_LINE('급여합계 : ' || emp_list.salary);
       END LOOP;

     EXCEPTION
       WHEN OTHERS THEN
          DBMS_OUTPUT.PUT_LINE(SQLERRM||'에러 발생 ');

   END;
   /

-- DBMS_OUTPUT.PUT_LINE을 출력하기 위해 사용
SQL> SET SERVEROUTPUT ON ; 

-- 실행
SQL> EXECUTE ForCursor_Test;
부서명 : ACCOUNTING
사원수 : 3
급여합계 : 8750
부서명 : RESEARCH
사원수 : 6
급여합계 : 10875
부서명 : SALES
사원수 : 6
급여합계 : 9305
```

### 6.2.3 명시적 커서의 속성(Explicit Cursor Attributes)
#### Explicit Cursor 속성
- %ISOPEN
    * 커서가 OPEN되어 있으면 TRUE
    * %ISOPEN속성을 이용하여 커서가 열려있는지 알 수 있다.
    
- %NOTFOUND
    * 패치한 데이터가 행을 반환하지 않으면 TRUE
    * %NOTFOUND 속성을 이용하여 루프를 종료할 시점을 찾는다.
    
- %FOUND
    * 패치한 데이터가 행을 반환하면 TRUE
    
- %ROWCOUNT
    * 현재까지 반환 된 모든 데이터 행의 수
    * %ROWCOUNT 속성을 이용하여 정확한 숫자만큼의 행을 추출한다.
    
```sql
CREATE OR REPLACE PROCEDURE AttrCursor_Test
    IS
    v_empno     emp.empno%TYPE;
    v_ename     emp.ename%TYPE;
    v_sal       emp.sal%TYPE;

    CURSOR emp_list IS
        SELECT empno, ename, sal
        FROM emp;

BEGIN

    DBMS_OUTPUT.ENABLE;

    OPEN emp_list;

    LOOP

        FETCH emp_list INTO v_empno, v_ename, v_sal;

        -- 데이터를 찾지 못하면 빠져 나갑니다
        EXIT WHEN emp_list%NOTFOUND;

    END LOOP;


    DBMS_OUTPUT.PUT_LINE('전체데이터 수 ' || emp_list%ROWCOUNT);

    CLOSE emp_list;

EXCEPTION
    WHEN OTHERS THEN
        DBMS_OUTPUT.PUT_LINE('ERR MESSAGE : ' || SQLERRM);

END;
/

-- DBMS_OUTPUT.PUT_LINE을 출력하기 위해 사용 
SQL> SET SERVEROUTPUT ON ;

-- 실행 결과
SQL> EXECUTE AttrCursor_Test;
전체데이터 수 15
```
### 6.2.4 파라미터가 있는 커서(Cursors with Parameters)
커서가 OPEN되고 질의가 실행되면 매개 변수 값을 커서에 전달할 수 있다.
다른 active set을 원할때 마다 explicit 커서를 따로 선언해야 한다.

#### 파라미터가 있는 커서 문법
```sql
CURSOR cursor_name
[(parameter_name datatype, ...)] 
IS
 SELECT statement
```

#### 파라키너가 있는 커서 예제
```sql
CREATE OR REPLACE PROCEDURE ParamCursor_Test
        (param_deptno   emp.deptno%TYPE) 
     IS

        v_ename     emp.ename%TYPE;

        -- Parameter가 있는 커서의 선언
        CURSOR emp_list(v_deptno emp.deptno%TYPE) IS
        SELECT ename 
        FROM emp
        WHERE deptno = v_deptno;

     BEGIN

        DBMS_OUTPUT.ENABLE;
        DBMS_OUTPUT.PUT_LINE(' ****** 입력한 부서에 해당하는 사람들 ****** ');              

        -- Parameter변수의 값을 전달(OPEN될 때 값을 전달한다)
        FOR emplst IN emp_list(param_deptno) LOOP    

          DBMS_OUTPUT.PUT_LINE('이름 : ' || emplst.ename);

        END LOOP;    

        EXCEPTION  
          WHEN OTHERS THEN
             DBMS_OUTPUT.PUT_LINE('ERR MESSAGE : ' || SQLERRM);         

     END; 
     /

--DBMS_OUTPUT.PUT_LINE을 출력하기 위해 사용
SQL> SET SERVEROUTPUT ON ; 

-- 실행 결과
SQL> EXECUTE ParamCursor_Test(10);
****** 입력한 부서에 해당하는 사람들 ******
이름 : CLARK
이름 : KING
이름 : MILLER
```

### 6.2.5 The WHERE CURRENT OF Clause
#### WHERE CURRENT OF
- ROWID를 이용하지 않고도 현재 참조하는 행을 갱신하고 삭제할 수 있게 한다.
- 추가적으로 FETCH문에 의해 가장 최근에 처리된 행을 참조하기 위해서 "WHERE CURRENT OF 커서이름" 절로 DELETE나 UPDATE문 작성이 가능하다.
- 이절을 사용할 대 참조하는 커서가 있어야 하며, FOR UPDATE절이 커서 선언 query문장 안에 있어야 한다. 그렇지 않으면 에러가 발생한다.

#### WHERE CURRENT OF 예제
```sql
CREATE OR REPLACE PROCEDURE where_current 
     IS

        CURSOR emp_list IS 
             SELECT empno 
             FROM emp 
             WHERE empno = 7934 
             FOR UPDATE; 

     BEGIN 

        --DBMS_OUTPUT.PUT_LINE명령을 사용하기 위해서 
        DBMS_OUTPUT.ENABLE;    

        FOR emplst IN emp_list LOOP
         
           --emp_list커서에 해당하는 사람의 직업을  SALESMAN으로 업데이트 시킵니다.
           UPDATE emp 
           SET job = 'SALESMAN' 
           WHERE CURRENT OF emp_list; 

           DBMS_OUTPUT.PUT_LINE('수정 성공');

        END LOOP; 
  
        EXCEPTION
           WHEN OTHERS THEN 
           -- 에러 발생시 에러 메시지 출력
           DBMS_OUTPUT.PUT_LINE('ERR MESSAGE : ' || SQLERRM); 
    END;  
    /

--먼저 데이터를 확인해 보자
SQL> SELECT job FROM emp WHERE empno = 7934;

JOB
---------
CLERK

 --PLSQL을 실행시키고..
SQL> EXECUTE where_current;
수정 성공 

-- 데이터를 확인하면 변경된 것을 확인 할 수 있다. 
SQL> SELECT job FROM emp WHERE empno = 7934;
 
JOB
---------
SALESMAN
```


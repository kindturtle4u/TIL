> 출처 : [gurubee.net](http://gurubee.net/oracle/plsql) Oracle PL/SQL 강좌 읽으면서 정리

# 3. PL/SQL 데이터 타입
## 3.1 스칼라 데이터 타입
스칼라 데이터타입에는 일반 단일 데이터타입의 변수와 %TYPE 데이터형 변수가 있다.

### 일반변수 선언 문법
identifier [CONSTANT] 데이터타입 [NOT NULL] :=상수 값이나 표현식

### 일반변수 선언 예제
```sql
-- 숫자형 상수 선언(변할 수 없다)
v_price CONTANT NUMBER(4,2) := 12.34 ;     

v_name VARCHAR2(20) ; 

v_Bir_Type  CHAR(1) ; 

-- NOT NULL의 TRUE로 초기화 
v_flag  BOOLEAN  NOT NULL := TRUE ;      

v_birthday DATE;
```
### %TYPE 데이터형
- 이미 선언된 다른 변수나 데이터베이스 컬럼의 데이터 타입을 이용하여 선언 한다.
- 데이터베이스 테이블과 컬럼 그리고 이미 선언한 변수명이 %TYPE앞에 올수 있다.

### %TYPE 사용 예제
```sql
SQL> CREATE OR REPLACE PROCEDURE Emp_Info
      -- IN Parameter 
      ( p_empno IN emp.empno%TYPE )

        IS

        -- %TYPE 데이터형 변수 선언 
        v_empno emp.empno%TYPE; 
        v_ename emp.ename%TYPE;
        v_sal   emp.sal%TYPE;

        BEGIN

        DBMS_OUTPUT.ENABLE;

        -- %TYPE 데이터형 변수 사용 
        SELECT empno, ename, sal
        INTO v_empno, v_ename, v_sal  
        FROM emp
        WHERE empno = p_empno ;

        -- 결과값 출력 
        DBMS_OUTPUT.PUT_LINE( '사원번호 : ' || v_empno ); 
        DBMS_OUTPUT.PUT_LINE( '사원이름 : ' || v_ename );
        DBMS_OUTPUT.PUT_LINE( '사원급여 : ' || v_sal );

        END;
       /

-- DBMS_OUTPUT 결과값을 화면에 출력 하기위해
SQL> SET SERVEROUTPUT ON;     

-- 실행 결과 
SQL> EXECUTE Emp_Info(7369); 

사원번호 : 7369
사원이름 : SMITH
사원급여 : 880
```

## 3.2. 복합 데이터 타입
### 3.2.1 %ROWTYPE
- 하나 이상의 데이터값을 갖는 데이터 타입으로 배열과 비슷한 역할을 하고 재사용이 가능하다.
- %ROWTYPE 데이터 형과, PL/SQL테이블과 레코드는 복합 데이터 타입에 속한다.

### %ROWTYPE
- 테이블이나 뷰 내부의 컬럼 데이터형, 크기, 속석등을 그대로 사용 할 수 있다.
- %ROWTYPE 앞에 오는 것은 데이터베이스 테이블 이름이다.
- 지정된 테이블의 구조와 동일한 구조를 갖는 변수를 선언 할 수 있다.

### %ROWTYPE 예제
```sql
SQL>CREATE OR REPLACE PROCEDURE RowType_Test
        ( p_empno IN emp.empno%TYPE )

    IS

        -- %ROWTYPE 변수 선언, 
        -- emp테이블의 속성을 그대로 사용할 수 있다. 
        v_emp   emp%ROWTYPE ;

   BEGIN

        DBMS_OUTPUT.ENABLE;

        -- %ROWTYPE 변수 사용 
        SELECT empno, ename, hiredate
        INTO v_emp.empno, v_emp.ename, v_emp.hiredate
        FROM emp
        WHERE empno = p_empno;

       DBMS_OUTPUT.PUT_LINE( '사원번호 : ' || v_emp.empno );
       DBMS_OUTPUT.PUT_LINE( '사원이름 : ' || v_emp.ename );
       DBMS_OUTPUT.PUT_LINE( '입 사 일 : ' || v_emp.hiredate );

   END;
   / 

-- DBMS_OUTPUT.PUT_LINE을 출력하기 위해 사용
SQL> SET SERVEROUTPUT ON ;  

-- 실행 결과 
SQL> EXECUTE RowType_Test(7900);

사원번호 : 7900
사원이름 : JAMES
입 사 일 : 81/12/03

PL/SQL 처리가 정상적으로 완료되었습니다.
```

### 3.2.3 PL/SQL 테이블
PL/SQL에서의 테이블은 오라클 SQL에서의 테이블과는 다르다.
PL/SQL에서의 테이블은 일종의 일차원 배열이라고 생각하면 이해하기 쉬울것이다.

- 테이블은 크기에 제한이 없으며 그 ROW의 수는 데이터가 들어옴에 따라 자동 증가한다.
- BINARY_INTEGER 타입의 인덱스 번호로 순서가 정해진다.
- 하나의 테이블에 한 개의 컬럼 데이터를 저장한다.

### PL/SQL 테이블 문법 및 선언 예제
TYPE table_name IS TABLE OF datatype INDEX BY BINARY INTEGER

identifier type_name :

```sql
-- 선언 예제
TYPE prdname_table IS TABLE OF VARCHAR2(30) 
INDEX BY BINARY_INTEGER; 

-- prdname_table 테이블타입으로 prdname_tab변수를 선언해서 사용 
prdname_tab   prdname_table 

-- 아래 프로시저에서 사용된 예제를 보면 이해가 쉽게 갈 것이다.
```

```sql
SQL> CREATE OR REPLACE PROCEDURE Table_Test
     (v_deptno IN emp.deptno%TYPE)

    IS

     -- 각 컬럼에서 사용할 테이블의 선언 
     TYPE empno_table IS TABLE OF emp.empno%TYPE
     INDEX BY BINARY_INTEGER;

     TYPE ename_table IS TABLE OF emp.ename%TYPE
     INDEX BY BINARY_INTEGER;

     TYPE sal_table IS TABLE OF emp.sal%TYPE
     INDEX BY BINARY_INTEGER;

     -- 테이블타입으로 변수를 선언해서 사용 
     empno_tab  empno_table ;
     ename_tab  ename_table ;
     sal_tab    sal_table;

     i BINARY_INTEGER := 0;

   BEGIN

     DBMS_OUTPUT.ENABLE;

     FOR emp_list IN(SELECT empno, ename, sal 
                     FROM emp WHERE deptno = v_deptno) LOOP

      /* emp_list는 자동선언되는 BINARY_INTEGER형 변수로 1씩 증가한다. 
         emp_list대신 다른 문자열 사용가능 */

            i := i + 1;

           -- 테이블 변수에 검색된 결과를 넣는다
            empno_tab(i) := emp_list.empno ;     
            ename_tab(i) := emp_list.ename ;
            sal_tab(i)   := emp_list.sal ;

      END LOOP;

      -- 1부터 i까지 FOR 문을 실행 
      FOR cnt IN 1..i LOOP

         -- TABLE변수에 넣은 값을 뿌려줌 
         DBMS_OUTPUT.PUT_LINE( '사원번호 : ' || empno_tab(cnt) );
         DBMS_OUTPUT.PUT_LINE( '사원이름 : ' || ename_tab(cnt) );
         DBMS_OUTPUT.PUT_LINE( '사원급여 : ' || sal_tab(cnt));

      END LOOP;

  END; 
  /

-- (DBMS_OUTPUT.PUT_LINE을 출력하기 위해 사용)
SQL> SET SERVEROUTPUT ON ;
 
실행 결과 
SQL> EXECUTE Table_Test(10);

사원번호 : 7782
사원이름 : CLARK
사원급여 : 2450
사원번호 : 7839
사원이름 : KING
사원급여 : 5000
사원번호 : 7934
사원이름 : MILLER
사원급여 : 1300 

PL/SQL 처리가 정상적으로 완료되었습니다. 

-- emp 테이블에 있는 데이터의 입력한 부서에 해당하는 사원번호, 
-- 사원이름, 사원급여를 뿌려주는 프로시저 이다 
```

### 3.2.3 PL/SQL 레코드 
- PL/SQL 레코드는 여러개의 데이터 타입을 갖는 변수들의 집합니다.
- 스칼라, RECORD, 또는 PL/SQL TABLE datatype중 하나 이상의 요소로 구성된다.
- 논리적 단위로서 필드 집합을 처리할 수 있도록 해 준다.
- PL/SQL 테이블과 다르게 개별 필드의 이름을 부여할 수 있고, 선언시 초기화가 가능한다.

### PL/SQL 레코드 문법 및 선언 예제
```sql
TYPE recode_name IS RECORD
(필드이름1 필드유형1 [NOT NULL {:= | DEFAULT} 식],
 필드이름2 필드유형2 [NOT NULL {:= | DEFAULT} 식],
 필드이름3 필드유형3 [NOT NULL {:= | DEFAULT} 식])
```

```sql
-- 선언 예제
TYPE record_test IS RECORD 
  ( record_empno   NUMBER, 
    record_ename   VARCHAR2(30), 
    record_sal     NUMBER); 

-- record_test 레코드 타입으로 prd_record변수를 선언해서 사용 
prd_record    record_test;

-- 아래 프로시저에서 사용된 예제를 보면 이해가 쉽게 갈 것이다.
```

```sql
SQL> CREATE OR REPLACE PROCEDURE Record_Test
      ( p_empno IN emp.empno%TYPE )

   IS

     -- 하나의 레코드의 세가지의 변수타입 선언 

     TYPE emp_record IS RECORD
     (v_empno    NUMBER,
      v_ename    VARCHAR2(30),
      v_hiredate  DATE );

     emp_rec   emp_record ;

   BEGIN

     DBMS_OUTPUT.ENABLE;

     -- 레코드의 사용 
     SELECT empno, ename, hiredate
     INTO emp_rec.v_empno, emp_rec.v_ename, emp_rec.v_hiredate
     FROM emp
     WHERE empno = p_empno;

     DBMS_OUTPUT.PUT_LINE( '사원번호 : ' || emp_rec.v_empno );
     DBMS_OUTPUT.PUT_LINE( '사원이름 : ' || emp_rec.v_ename );
     DBMS_OUTPUT.PUT_LINE( '입 사 일 : ' || emp_rec.v_hiredate);

   END;
 / 

-- (DBMS_OUTPUT.PUT_LINE을 출력하기 위해 사용)
SQL> SET SERVEROUTPUT ON ;  

-- 실행 결과 
SQL> EXECUTE Record_Test(7369);

사원번호 : 7369
사원이름 : SMITH
입 사 일 : 80/12/17

PL/SQL 처리가 정상적으로 완료되었습니다. 

-- %ROWTYPE 예제와 비교해 보세요 
```

### 3.2.4 PL/SQL Table of Record
- PL/SQL TABLE변수 선언과 비슷하며 데이터타입을 %ROWTYPE으로 선언하면 된다.
- PL/SQL TABLE과 RECORD의 복합 기능을 한다.

### PL/SQL TABLE Of Record 문법
```sql
-- 선언 예제
TYPE dept_table_type IS TABLE OF dept%ROWTYPE
INDEX BY BINARY_INTEGER; 

-- Each element of dept_table  is a record 
dept_table   dept_table_type;

-- 아래 프로시저에서 사용된 예제를 보면 이해가 쉽게 갈 것이다.
```

```sql
CREATE OR REPLACE PROCEDURE Table_Test
  IS
  
    i BINARY_INTEGER := 0;
 
    -- PL/SQL Table of Record의 선언
    TYPE dept_table_type IS TABLE OF dept%ROWTYPE
    INDEX BY BINARY_INTEGER;
 
    dept_table dept_table_type;
 
  BEGIN
  
    FOR dept_list IN (SELECT * FROM dept) LOOP
 
      i:= i+1;
  
      -- TABLE OF RECORD에 데이터 보관
      dept_table(i).deptno := dept_list.deptno ;     
      dept_table(i).dname := dept_list.dname ;
      dept_table(i).loc   := dept_list.loc ;
 
    END LOOP;
 
    FOR cnt IN 1..i LOOP
 
       -- 데이터 출력
       DBMS_OUTPUT.PUT_LINE( '부서번호 : ' || dept_table(cnt).deptno || 
                             '부서명 : ' ||  dept_table(cnt).dname || 
                               '위치 : ' || dept_table(cnt).loc );
 
    END LOOP;
 
 END;
/
 
 
SQL> SET SERVEROUTPUT ON ;

SQL> EXECUTE Table_test;
부서번호 : 10부서명 : ACCOUNTING위치 : NEW_YORK
부서번호 : 20부서명 : RESEARCH위치 : DALLAS
부서번호 : 30부서명 : 인사과위치 : CHICAGO
부서번호 : 40부서명 : OPERATIONS위치 : BOS%TON
 
PL/SQL 처리가 정상적으로 완료되었습니다.
    
```
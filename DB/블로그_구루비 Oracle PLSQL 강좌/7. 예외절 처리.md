> 출처 : [gurubee.net](http://gurubee.net/oracle/plsql) Oracle PL/SQL 강좌 읽으면서 정리

# 7. 예외절 처리
## 7.1 예외 (Exception)
#### 예외(Exception)란?
오라클 PL/SQL의 오류를 예외라고 한다.
오류는 PL/SQL을 컴파일 할때 문법적인 오류로 발생하는 컴파일 타임 오류와,
프로그램을 실행 할 때 발생하는 실행타입 오류로 구분 할 수 있다.

#### PL/SQL 오류의 종류
- 미리 정의된 오라클 서버 오류(Predefined Oracle Server)
    * 설명 : PL/SQL에서 자주 발생하는 약 20개의 오류
    * 처리 : 선언할 필요도 없고, 발생시에 예외 절로 자동 트랩(Trap) 된다.
    
- 미리 정의되지 않은 오라클 서버 오류(Non-Predefined Oracle Server)
    * 설명 : 미리 정의된 오라클 서버 오류를 제외한 모든 오류
    * 처리 : 선언부에서 선언해야 하고 발생시 자동 트랩된다.
    
- 사용자 정의 오류(User-Defined)
    * 설명 : 개발자가 정한 조건에 만족하지 않을경우 발생하는 오류
    * 처리 : 선언부에서 선언하고 실행부에서 RAISE문을 사용하여 발생시켜야 한다.
    
### 예외처리 문법
```sql
EXCEPTION
WHEN 예외1[OR 예외2] THEN
    statements 1
    statements 2..
[WHEN 예외3[OR 예외4] THEN
    statements1.. ]
[WHEN OTHERS THEN
    statements1.. ]
```
- 예외 처리절은 EXCEPTION부터 시작 한다.
- 예외가 발생하면 여러 개의 예외 처리부 중에 하나의 예외 처리부에 트랩(Trap) 된다.
- WHEN OTHERS 절은 맨 마지막에 온다.

## 7.2 미리 정의된 예외(Predefined Exceptions)
오라클 PL/SQL은 자주 일어나는 몇가지 예외를 미리 정의해 놓았으며, 이러한 예외는 개발자가 따로 선언할 필요가 없다.

#### 미리 정의된 예외의 종류
- NO_DATA_FOUND : SELECT 문이 아무런 데이터 행을 반환하지 못할때
- DUP_VAL_ON_INDEX : UNIQUE 제약을 갖는 컬럼에 중복되는 데이터가 INSERT 될 때
- ZERO_DIVIDE : 0으로 나눌 때
- INVALID_CURSOR : 잘못된 커서 연산
- [Predefined PL/SQL Exceptions 더 보기](https://docs.oracle.com/cd/B28359_01/appdev.111/b28370/errors.htm#LNPLS00703)

#### 미리 정의된 예외 예제
```sql
CREATE OR REPLACE PROCEDURE PreException_test
         (v_deptno  IN emp.deptno%TYPE)  
   IS
       v_emp   emp%ROWTYPE;
   BEGIN
      DBMS_OUTPUT.ENABLE;

      SELECT empno, ename, deptno
      INTO v_emp.empno, v_emp.ename, v_emp.deptno
      FROM emp
      WHERE deptno = v_deptno ;

      DBMS_OUTPUT.PUT_LINE('사번 : ' || v_emp.empno);
      DBMS_OUTPUT.PUT_LINE('이름 : ' || v_emp.ename);
      DBMS_OUTPUT.PUT_LINE('부서번호 : ' || v_emp.deptno);

   EXCEPTION

      WHEN   DUP_VAL_ON_INDEX   THEN
          DBMS_OUTPUT.PUT_LINE('데이터가 존재 합니다.');
          DBMS_OUTPUT.PUT_LINE('DUP_VAL_ON_INDEX 에러 발생');
      WHEN   TOO_MANY_ROWS   THEN  
        DBMS_OUTPUT.PUT_LINE('TOO_MANY_ROWS에러 발생');
      WHEN   NO_DATA_FOUND   THEN  
        DBMS_OUTPUT.PUT_LINE('NO_DATA_FOUND에러 발생');
      WHEN OTHERS THEN 
        DBMS_OUTPUT.PUT_LINE('기타 에러 발생');

  END;
  / 

-- DBMS_OUTPUT.PUT_LINE을 출력하기 위해 사용
SQL> SET SERVEROUTPUT ON ;  

-- 프로시저 실행
SQL> EXECUTE PreException_Test(20);
TOO_MANY_ROWS에러 발생

-- TOO_MANY_ROWS 에러가 발생하는 이유?
 - SELECT문의 결과가 1개 이상의 행을 리턴하기 때문이다..
 - TOO_MANY_ROWS를 피하기 위해서는 FOR문이나 LOOP문으로 SELECT문을 처리해야 한다.

--아래와 같이 변경하면 에러가 발생하지 않는다.

  FOR  emp_list  IN
      (SELECT empno, ename, deptno
       FROM emp
       WHERE deptno = v_deptno)   LOOP

      DBMS_OUTPUT.PUT_LINE('사번 : ' || emp_list.empno);
      DBMS_OUTPUT.PUT_LINE('이름 : ' || emp_list.ename);
      DBMS_OUTPUT.PUT_LINE('부서번호 : ' || emp_list.deptno);

  END LOOP;
```
## 7.3 미리 정의되지 않은 예외(Non-Predefined Exception)
#### 미리 정의되지 않은 예외란?
미리정의되지 않은 예외는 사용자가 선언절에서 예외명을 정의하고, 
Oracle Server에서 제공하는 Error번호를 사용하여, 
정의된 예외와 연결한 후 EXCEPTION절에서 정의된 예외를 사용한다.

- STEP 1 : 예외의 이름을 선언(선언절)
- STEP 2 : PRAGMA EXCEPTION_INIT 문장으로 예외의 이름과 오라클 서버 오류 번호를 결합(선언절)
- STEP 3 : 예외가 발생할 경우 해당 예외를 참조한다(예외절)

#### 미리 정의되지 않은 예외 예제
```sql
CREATE OR REPLACE PROCEDURE NonPreException_Test 
       IS
          not_null_test    EXCEPTION; -- STEP 1
          /* not_null_test는 선언된 예외 이름 
             -1400 Error 처리번호는 표준 Oracle7 Server Error 번호 */
          PRAGMA EXCEPTION_INIT(not_null_test, -1400);     -- STEP 2

        BEGIN

          DBMS_OUTPUT.ENABLE;

        -- empno를 입력하지 않아서 NOT NULL 에러 발생
        INSERT INTO emp(ename, deptno)
        VALUES('tiger', 30);

        EXCEPTION

        WHEN not_null_test THEN    -- STEP 3

            DBMS_OUTPUT.PUT_LINE('not null 에러 발생 ');

       END;
        /

-- DBMS_OUTPUT.PUT_LINE을 출력하기 위해 사용
SQL> SET SERVEROUTPUT ON ; 

-- 실행 결과 
SQL> EXECUTE NonPreException_Test;
not null 에러 발생
```

## 7.4 사용자 정의 예외(User-Defined Exptions)
오라클 저장함수 RAISE_APPLICATION_ERROR를 사용하여 오류코드 -20000qnxj -20999의 범위 내에서 사용자 정의 예외를 만들수 있다.
- STEP 1 : 예외 이름을 선언(선언절)
- STEP 2 : RAISE문을 사용하여 직접적으로 예외를 발생시킨다(실행절)
- STEP 3 : 예외가 발생할 경우 해당 예외를 참조한다(예외절)

#### 사용자 정의 예외 예제
```sql
CREATE OR REPLACE PROCEDURE User_Exception 
        (v_deptno IN emp.deptno%type ) 
     IS

       -- 예외의 이름을 선언
       user_define_error EXCEPTION;     -- STEP 1
       cnt     NUMBER;

     BEGIN

       DBMS_OUTPUT.ENABLE;   

       SELECT COUNT(empno) 
       INTO cnt
       FROM emp
       WHERE deptno = v_deptno;

       IF cnt < 5 THEN
         -- RAISE문을 사용하여 직접적으로 예외를 발생시킨다
          RAISE user_define_error;         -- STEP 2
       END IF;

      EXCEPTION
        -- 예외가 발생할 경우 해당 예외를 참조한다.
       WHEN user_define_error THEN      -- STEP 3
           RAISE_APPLICATION_ERROR(-20001, '부서에 사원이 몇명 안되네요..');

   END; 
  /

-- DBMS_OUTPUT.PUT_LINE을 출력하기 위해 사용 
SQL> SET SERVEROUTPUT ON ; 

-- 10부서의 사원이 5보다 적기 때문에 사용자 정의 예외가 발생.
SQL> EXECUTE user_exception(10);
BEGIN user_exception(10); END;
 *
1행에 오류:
ORA-20001: 부서에 사원이 몇명 안되네요..
ORA-06512: "SCOTT.USER_EXCEPTION", 줄 17에서
ORA-06512: 줄 1에서

-- 20부서로 실행을 하면 에러가 발생하지 않는 것 을 알 수 있다.
SQL> EXECUTE user_exception(20);
PL/SQL 처리가 정상적으로 완료되었습니다.
```
## 7.5 SQLCODE, SQLERRM
`SQLCODE`, `SQLERRM` 구분을 사용해서 `WHEN OTHERS`문으로 트랩(Trpa)되는 오류들의 실제 오류 코드와 설명을 볼 수 있다.

- SQLCODE : 실행된 프로그램이 성공적으로 종료하였을때는 오류번호 0을 포함하며, 그렇지 못할 경우세는 해당 오류코드 번호를 포함한다.
- SQLERRM : SQLCODE에 포함된 오라클 오류번호에 해당하는 메시지를 가진다.

#### SQLCODE, SQLERRM 예제
```sql
CREATE OR REPLACE PROCEDURE Errcode_Exception 
        (v_deptno IN emp.deptno%type ) 
     IS

         v_emp   emp%ROWTYPE ;  

     BEGIN  

         DBMS_OUTPUT.ENABLE;

         -- ERROR발생 for문을 돌려야 됨
         SELECT * 
         INTO v_emp
         FROM emp
         WHERE deptno = v_deptno;
      
         DBMS_OUTPUT.PUT_LINE('사번 : ' || v_emp.empno);    
         DBMS_OUTPUT.PUT_LINE('이름 : ' || v_emp.ename);    
          
     EXCEPTION
      
     WHEN OTHERS THEN

          DBMS_OUTPUT.PUT_LINE('ERR CODE : ' || TO_CHAR(SQLCODE));
          DBMS_OUTPUT.PUT_LINE('ERR MESSAGE : ' || SQLERRM);

  END;  
  /

-- DBMS_OUTPUT.PUT_LINE을 출력하기 위해 사용
SQL> SET SERVEROUTPUT ON; 

-- 실행예제
SQL> EXECUTE Errcode_Exception(30);
ERR CODE : -1422
ERR MESSAGE : ORA-01422: 실제 인출은 요구된 것보다 많은 수의 행을 추출합니다
```